{ pkgs
, ormolu
, cpphs
}:
{ package
, expectedFailures ? null
, doCheck ? false
}:
pkgs.stdenv.mkDerivation rec {
  name = package.name + "-ormolized";
  src = package.src;
  buildInputs = [
    ormolu
    cpphs
    pkgs.diffutils
    pkgs.glibcLocales
  ];
  LANG = "en_US.UTF-8";
  buildPhase = ''
    hs_files=$(find . -name '*.hs' -o -name '*.hsig')
    for hs_file in $hs_files; do

      # drop includes
      sed -i '/^#include/d' "$hs_file"

      # Deal with CPP. Some packages guard against non-GHC compilers with
      # an #error, which cpphs hits and gives up on because we do not define
      # __GLASGOW_HASKELL__ for it. Keep the original file in that case
      # rather than failing the whole derivation.
      if cpphs "$hs_file" --noline -DARCH_X86 > "''${hs_file}-nocpp" 2> /dev/null; then
        # annoyingly, cpphs cannot modify files in place
        mv "''${hs_file}-nocpp" "$hs_file"
      else
        rm -f "''${hs_file}-nocpp"
      fi

      # preserve the original
      cp "$hs_file" "''${hs_file}-original"
    done

    ((ormolu --check-idempotence --mode inplace $hs_files; echo $? > exit_code) || true) 2> log.txt
  '';
  inherit doCheck;
  checkPhase =
    if expectedFailures == null
    then ''
      echo "No failures expected"
      if (( $(cat exit_code) != 0 )); then exit 1; fi
    ''
    else ''
      diff --ignore-blank-lines --color=always ${expectedFailures} log.txt
    '';
  installPhase = ''
    mkdir "$out"
    find . \( -name '*.hs-original' -o -name '*.hs' -o -name '*.hsig-original' -o -name '*.hsig' -o -name '*.cabal' \) -exec cp --parents {} $out \;
    cp log.txt $out/log.txt
  '';
}
